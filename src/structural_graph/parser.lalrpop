use crate::structural_graph::ast::*;
use super::Symbol;

grammar;

pub Graph: Vec<Entry> = {
    Node*
}

Node: Entry = {
    <Type> <Str> "[" <Comma<AdjencyTuple>> "]" => Entry::new(<>),
}

Type: EntryType = {
    "Port" => EntryType::Port,
    "DataReg" => EntryType::DataReg,
    "NullReg" => EntryType::NullReg,
    "ControlReg" => EntryType::ControlReg,
}

Str: Symbol = {
    <s:r#""[^"]*""#> => Symbol::from(&s[1 .. s.len()-1]),
}

Num: f64 = {
    <i:r"[0-9]+(?:\.[0-9]+)?"> => i.parse().unwrap(),
}

AdjencyTuple: (Symbol, f64) = {
    "(" <s:Str> "," <n:Num> ")" => (s, n),
}

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

// Local Variables:
// mode: rust
// End:
